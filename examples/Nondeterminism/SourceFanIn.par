dec PlayFanIn : [List<Source<!, String>>] Source<(!) List<!>, String>
def PlayFanIn = [sources] Source.FanIn(sources)

type Source<e, a> = recursive choice {
  .close => Result<e, !>,
  .next => either {
    .end Result<e, !>,
    .item(a) self,
  }
}

type Source.Fan<e, a> = recursive either {
  .end!,
  .split(self) self,
  .client choice {
    .close => Result<e, !>,
    .next => either {
      .end Result<e, !>,
      .item(a) self,
    }
  }
}

dec Source.FanIn : <e, a>[List<Source<e, a>>] Source<(e) List<e>, a>
def Source.FanIn = <e, a>[sources]
  let fan: Source.Fan<e, a> = sources.begin.case {
    .end! => .end!,
    .item(source) sources => .split(do { source.begin } in .client case {
      .close => source.close,
      .next => source.next.case {
        .end result => .end result,
        .item(value) source => .item(value) source.loop,
      }
    }) sources.loop,
  } in poll(fan) {
    fan => fan.case {
      .end! => submit(),
      .split(fan1) fan2 => submit(fan1, fan2),

      .client session => case {
        .close =>
          let state: Result<(e) List.Builder<e>, !> = .ok! in
          repoll(type Source.Fan<e, a> in .client session) {
            fan => fan.case {
              .end! => submit(),
              .split(fan1) fan2 => submit(fan1, fan2),
              .client session => session.close.case {
                .ok! => submit(),
                .err e => state.case {
                  .ok! => let state = .err(e) List.Builder(type e) in submit(),
                  .err(e0) es => let state = .err(e0) es.add(e) in submit(),
                }
              }
            }
            else => state.case {
              .ok! => .ok!,
              .err(e0) es => .err(e0) es.build,
            }
          }

        .next => session.next.case {
          .end result => 
            let state: Result<(e) List.Builder<e>, !> = result.case {
              .ok! => .ok!,
              .err e => .err(e) List.Builder(type e),
            } in
            repoll@next() {
              fan => fan.case {
                .end! => submit@next(),
                .split(fan1) fan2 => submit@next(fan1, fan2),
                .client session => state.case {
                  .ok! => session.next.case {
                    .end result => result.case {
                      .ok! => let state = .ok! in submit@next(),
                      .err e => let state = .err(e) List.Builder(type e) in submit@next(),
                    }
                    .item(value) fan => .item(value) submit(fan),
                  }
                  .err(e0) es => session.close.case {
                    .ok! => let state = .err(e0) es in submit@next(),
                    .err e => let state = .err(e0) es.add(e) in submit@next(),
                  }
                }
              }
              else => state.case {
                .ok! => .end .ok!,
                .err(e0) es => .end .err(e0) es.build,
              }
            }

          .item(value) fan => .item(value) submit(fan),
        }
      }
    }

    else => case {
      .close => .ok!,
      .next => .end .ok!,
    }
  }
